apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.symphony.technicalassessment"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

def dependencyGroup(Closure closure) {
    closure.delegate = dependencies
    return closure
}

def support = dependencyGroup {
    final SUPPORT_VERSION = '28.0.0'
    implementation "com.android.support:appcompat-v7:$SUPPORT_VERSION"
    implementation "com.android.support:recyclerview-v7:$SUPPORT_VERSION"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
}

def test = dependencyGroup {
    testImplementation 'junit:junit:4.12'
}

def network = dependencyGroup {
    final RETROFIT_VERSION = '2.4.0'
    implementation("com.squareup.retrofit2:retrofit:$RETROFIT_VERSION") {
        // exclude Retrofitâ€™s OkHttp peer-dependency module and define your own module import
        exclude module: 'okhttp'
    }
    implementation "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$RETROFIT_VERSION"

    final OKHTTP_VERSION = '3.11.0'
    implementation "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"
    implementation group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: "$OKHTTP_VERSION"
    implementation("com.squareup.okhttp3:okhttp-urlconnection:$OKHTTP_VERSION") {
        exclude group: 'org.json'
    }
}

def reactive = dependencyGroup {
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.15'
}

def dependencyInjection = dependencyGroup {
    final DAGGER_VERSION = '2.19'
    //Dagger 2
    implementation "com.google.dagger:dagger:$DAGGER_VERSION"
    annotationProcessor "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    //Android dagger
    implementation "com.google.dagger:dagger-android-support:$DAGGER_VERSION"
    annotationProcessor "com.google.dagger:dagger-android-processor:$DAGGER_VERSION"
}

def thirdPartLibrary = dependencyGroup {
    //Butterknife
    final BUTTER_KNIFE_VERSION = '9.0.0-rc2'
    implementation "com.jakewharton:butterknife:$BUTTER_KNIFE_VERSION"
    annotationProcessor "com.jakewharton:butterknife-compiler:$BUTTER_KNIFE_VERSION"

    //Immutables
    final IMMUTABLES = '2.7.3'
    compileOnly "org.immutables:value:$IMMUTABLES"
    annotationProcessor "org.immutables:value:$IMMUTABLES"
    implementation "org.immutables:gson:$IMMUTABLES"

    //Sectioned RecyclerView
    implementation 'com.truizlop.sectionedrecyclerview:library:1.2.0'

    //Glide
    implementation "com.github.bumptech.glide:glide:4.8.0"
    implementation ("jp.wasabeef:glide-transformations:3.3.0") {
        exclude group: 'com.github.bumptech.glide'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    support()
    network()
    reactive()
    dependencyInjection()
    thirdPartLibrary()
    test()
}
